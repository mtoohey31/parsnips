file = { SOI ~ section* ~ EOI }
section = _{ dataSection | textSection }

label = @{ ASCII_ALPHANUMERIC+ ~ ":" }

dataSection = { ".data" ~ (label ~ size ~ value)* }
size = @{ "." ~ ASCII_ALPHANUMERIC+ }
value = { string | array | int }
array = { int ~ ":" ~ int }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

textSection = { ".text" ~ (label | instruction)* }
instruction = { "syscall" | (instructionIdent ~ (arg ~ ("," ~ arg)*)?) }
instructionIdent = @{ ASCII_ALPHANUMERIC ~ (ASCII_ALPHANUMERIC | ".")* }
arg = _{ reg | offsetReg | dataIdent | int }
reg = @{ "$" ~ ASCII_ALPHANUMERIC+ }
offsetReg = @{ int ~ "(" ~ reg ~ ")" }
dataIdent = @{ ASCII_ALPHANUMERIC+ }
int = @{ "-"? ~ ASCII_DIGIT+ }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ ("#" | ";") ~ (!"\n" ~ ANY)* }
